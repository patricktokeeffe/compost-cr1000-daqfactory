'Acquire Data from soil probe AND thermocouples, AND send To DAQFactory
' 
'Copyright 2019 Patrick O'Keeffe <pokeeffe@wsu.edu> for Washington State University
'
'Permission to use, copy, modify, and/or distribute this software for any purpose with or without fee is hereby granted, provided that the above copyright notice and this permission notice appear in all copies.
'
'THE SOFTWARE Is PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH REGARD To THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE For ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, Data OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.


'Specify thermocouple type
'Valid options = {TypeT, TypeE, TypeK, TypeJ, TypeB, TypeR, TypeS, TypeN}
Const TC_TYPE = TypeK

'Specify program timing
Const SCAN_INTV = 5 'seconds, how frequently to query sensors
Const DATA_INTV = 5 'seconds, how often to write data to table/serial port

' T/RH probe
' wired to SDI-12 input C1
Public cs215(2)
Alias cs215(1) = CS215_T
Alias cs215(2) = CS215_RH
Units CS215_T = degC
Units CS215_RH = percent

' Thermocouple inputs, DF1-4 respectively
Public panel_tmpr
Public tcdata(4)
Alias tcdata(1) = TC1_tmpr  'bottom temp
Alias tcdata(2) = TC2_tmpr  'middle temp
Alias tcdata(3) = TC3_tmpr
Alias tcdata(4) = TC4_tmpr  'air temp
Units panel_tmpr = degC
Units tcdata = degC

Const SERIAL_BUFF = 80
Const CRLF = CHR(13) & CHR(10) '<CR><LF> delimiter
Dim outdata(6)

DataTable(compost_soil,True,-1)
  'No cardout() instruction
  DataInterval(0,DATA_INTV,Sec,5)
  Average(1,TC1_tmpr,IEEE4,TC1_tmpr=NAN) 'outdata(1)
  Average(1,TC2_tmpr,IEEE4,TC2_tmpr=NAN)
  Average(1,TC3_tmpr,IEEE4,TC3_tmpr=NAN)
  Average(1,TC4_tmpr,IEEE4,TC4_tmpr=NAN)
  Average(1,CS215_T,IEEE4,CS215_T=NAN)
  Average(1,CS215_RH,IEEE4,CS215_RH=NAN)
 EndTable
 
DisplayMenu("Compost sensors",-2)
  DisplayValue("TC DF1", TC1_tmpr)
  DisplayValue("TC DF2", TC2_tmpr)
  DisplayValue("TC DF3", TC3_tmpr)
  DisplayValue("TC DF4", TC4_tmpr)
  DisplayValue("Tmpr", CS215_T)
  DisplayValue("RH", CS215_RH)
EndMenu

Sub report_to_daqfactory()
  'Create and send strings to serial port "ComRS232" (front panel DB9)
  Dim msg As String * SERIAL_BUFF
  
  Sprintf(msg,"TC,%f,%f,%f,%f,%f,%f",outdata(1),outdata(2),outdata(3),outdata(4),outdata(5),outdata(6))
  SerialOut(ComRS232,msg & CRLF,"",0,0)
  Delay(1,1,Sec)
EndSub

BeginProg
  SerialOpen(ComRS232,115200,0,0,SERIAL_BUFF)

	Scan (SCAN_INTV,Sec,0,0)

	SDI12Recorder(cs215(),1,"0","M!",1,0)

    PanelTemp(panel_tmpr,_60Hz)
    TCDiff(tcdata,4,mV25,1,TC_TYPE,panel_tmpr,1,0,_60Hz,1,0)

	 CallTable(compost_soil)
    If (compost_soil.Output(1,1)) Then
      GetRecord(outdata(1),compost_soil,1)
      report_to_daqfactory()
    EndIf
	NextScan
EndProg
   >        